---
output:
  github_document:
    md_extensions: "+ignore_line_breaks"
---

# 9章 …… もう一度、もう一度、もう一度、……

```{r setup, include=FALSE}
library(knitscm)
handle <- run_gauche()
knitr::opts_chunk$set(handle = handle)
```

```{scm include=FALSE}
(load "./little.scm")
```

関数`keep-looking`

```{scm results=FALSE}
(define keep-looking
  (lambda (a sorn lat)
    (cond
     ((number? sorn)
      (keep-looking a (pick sorn lat) lat))
     (else (eq? sorn a)))))
```

関数`looking`

```{scm results=FALSE}
(define looking
  (lambda (a lat)
    (keep-looking a (pick 1 lat) lat)))
```

```{scm}
(looking 'caviar '(6 2 4 caviar 5 7 3))
```

```{scm}
(looking 'caviar '(6 2 grits caviar 5 7 3))
```

関数`shift`

```{scm results=FALSE}
(define shift
  (lambda (pair)
    (build (first (first pair))
           (build (second (first pair))
                  (second pair)))))
```

```{scm}
(shift '((a b) c))
```

```{scm}
(shift '((a b) (c d)))
```

関数`align`

```{scm results=FALSE}
(define align
  (lambda (para)
    (cond
     ((atom? para) para)
     ((a-pair? (first para))
      (align (shift para)))
     (else (build (first para)
                  (align (second para)))))))
```

`align`の引数中のアトムの数を数える関数`length*`

```{scm results=FALSE}
(define length*
  (lambda (para)
    (cond
     ((atom? para) 1)
     (else
      (+ (length* (first para))
         (length* (second para)))))))
```

```{scm}
(length* '((a b) c))
```

関数`weight*`

```{scm results=FALSE}
(define weight*
  (lambda (para)
    (cond
     ((atom? para) 1)
     (else
      (+ (* (weight* (first para)) 2)
         (weight* (second para)))))))
```

```{scm}
(weight* '((a b) c))
```

```{scm}
(weight* '(a (b c)))
```

`align`の引数の`weight*`が次第に小さくなるので`align`は全関数、ということらしい。

関数`shuffle`

```{scm results=FALSE}
(define shuffle
  (lambda (para)
    (cond
     ((atom? para) para)
     ((a-pair? (first para))
      (shuffle (revpair para)))
     (else (build (first para)
                  (shuffle (second para)))))))
```

```{scm}
(shuffle '(a (b c)))
```

```{scm}
(shuffle '(a b))
```

```{scm eval=FALSE}
(shuffle '((a b) '(c d)))
```

答えが出ないので`shuffle`は全関数ではない。

関数`C`

```{scm results=FALSE}
(define C
  (lambda (n)
    (cond
     ((one? n) 1)
     ((even? n) (C (÷ n 2)))
     (else (C (add1 (× 3 n)))))))
```

これは[コラッツの問題](https://ja.wikipedia.org/wiki/%E3%82%B3%E3%83%A9%E3%83%83%E3%83%84%E3%81%AE%E5%95%8F%E9%A1%8C)という未解決問題。


コラッツ数列を得る関数`Cseq`

```{scm results=FALSE}
(define Cseq
  (lambda (n)
    (cond
     ((one? n) '(1))
     ((even? n) (cons n (Cseq (÷ n 2))))
     (else (cons n (Cseq (add1 (× 3 n))))))))
```

```{scm}
(Cseq 6)
```

関数`A` ([アッカーマン関数](https://ja.wikipedia.org/wiki/%E3%82%A2%E3%83%83%E3%82%AB%E3%83%BC%E3%83%9E%E3%83%B3%E9%96%A2%E6%95%B0))

```{scm results=FALSE}
(define A
  (lambda (n m)
    (cond
     ((zero? n) (add1 m))
     ((zero? m) (A (sub1 n) 1))
     (else (A (sub1 n)
              (A n (sub1 m)))))))
```

```{scm}
(A 1 1)
```

```{scm}
(A 2 2)
```
