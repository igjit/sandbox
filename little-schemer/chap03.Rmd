---
output:
  github_document:
    md_extensions: "+ignore_line_breaks"
---

# 3章 偉大なるCons

```{r setup, include=FALSE}
library(knitscm)
handle <- run_gauche()
knitr::opts_chunk$set(handle = handle)
```

p.35の未完成な`rember`

```{scm results=FALSE}
(define rember
  (lambda (a lat)
    (cond
     ((null? lat) (quote ()))
     (else (cond
            ((eq? (car lat) a) (cdr lat))
             (else (rember a (cdr lat))))))))
```

baconを除く

```{scm}
(rember 'bacon '(bacon lettuce and tomato))
```

andを除いたときにandの前にある全ての要素を失ってしまう

```{scm}
(rember 'and '(bacon lettuce and tomato))
```

p.38の`rember`

```{scm results=FALSE}
(define rember
  (lambda (a lat)
    (cond
     ((null? lat) (quote ()))
     (else (cond
            ((eq? (car lat) a) (cdr lat))
            (else (cons (car lat)
                        (rember a (cdr lat)))))))))
```

```{scm}
(rember 'and '(bacon lettuce and tomato))
```

p.41の簡単化した`rember`

```{scm results=FALSE}
(define rember
  (lambda (a lat)
    (cond
     ((null? lat) (quote ()))
     ((eq? (car lat) a) (cdr lat))
     (else (cons (car lat)
                 (rember a (cdr lat)))))))
```

```{scm}
(rember 'and '(bacon lettuce and tomato))
```

```{scm}
(rember 'sauce '(soy sauce and tomato sauce))
```

関数`firsts`

```{scm results=FALSE}
(define firsts
  (lambda (l)
    (cond
     ((null? l) (quote ()))
     (else (cons (car (car l)) (firsts (cdr l)))))))
```

```{scm}
(firsts '((a b) (c d) (e f)))
```

関数`insertR`

```{scm results=FALSE}
(define insertR
  (lambda (new old lat)
    (cond
     ((null? lat) (quote ()))
     (else
      (cond
       ((eq? (car lat) old)
        (cons old
              (cons new (cdr lat))))
       (else (cons (car lat)
                   (insertR new old
                            (cdr lat)))))))))
```

```{scm}
(insertR 'topping 'fudge '(ice cream with fudge for dessert))
```

関数`insertL`

```{scm results=FALSE}
(define insertL
  (lambda (new old lat)
    (cond
     ((null? lat) (quote ()))
     (else
      (cond
       ((eq? (car lat) old)
        (cons new lat))
       (else (cons (car lat)
                   (insertL new old
                            (cdr lat)))))))))
```

```{scm}
(insertL 'x 'b '(a b c d))
```

関数`subst`

```{scm results=FALSE}
(define subst
  (lambda (new old lat)
    (cond
     ((null? lat) (quote ()))
     (else
      (cond
       ((eq? (car lat) old)
        (cons new (cdr lat)))
       (else (cons (car lat)
                   (subst new old
                            (cdr lat)))))))))
```

```{scm}
(subst 'topping 'fudge '(ice cream with fudge for dessert))
```

関数`subst2`

```{scm results=FALSE}
(define subst2
  (lambda (new o1 o2 lat)
    (cond
     ((null? lat) (quote ()))
     (else
      (cond
       ((or (eq? (car lat) o1)
            (eq? (car lat) o2))
        (cons new (cdr lat)))
       (else (cons (car lat)
                   (subst2 new o1 o2
                           (cdr lat)))))))))
```

```{scm}
(subst2 'vanilla 'chocolate 'banana '(banana ice creamwith chocolate topping))
```

関数`multirember`

```{scm results=FALSE}
(define multirember
  (lambda (a lat)
    (cond
     ((null? lat) (quote ()))
     ((eq? (car lat) a)
      (multirember a (cdr lat)))
     (else (cons (car lat)
                 (multirember a (cdr lat)))))))
```

```{scm}
(multirember 'cup '(coffee cup tea cup and hick cup))
```

関数`multiinsertR`

```{scm results=FALSE}
(define multiinsertR
  (lambda (new old lat)
    (cond
     ((null? lat) (quote ()))
     ((eq? (car lat) old)
      (cons old
            (cons new
                  (multiinsertR new old (cdr lat)))))
     (else (cons (car lat)
                 (multiinsertR new old (cdr lat)))))))
```

```{scm}
(multiinsertR 'x 'b '(a b c d b e))
```

関数`multiinsertL`

```{scm results=FALSE}
(define multiinsertL
  (lambda (new old lat)
    (cond
     ((null? lat) (quote ()))
     ((eq? (car lat) old)
      (cons new
            (cons old
                  (multiinsertL new old (cdr lat)))))
     (else (cons (car lat)
                 (multiinsertL new old (cdr lat)))))))
```

```{scm}
(multiinsertL 'x 'b '(a b c d b e))
```

関数`multisubst`

```{scm results=FALSE}
(define multisubst
  (lambda (new old lat)
    (cond
     ((null? lat) (quote ()))
     ((eq? (car lat) old)
      (cons new (multisubst new old (cdr lat))))
     (else (cons (car lat)
                 (multisubst new old (cdr lat)))))))
```

```{scm}
(multisubst 'x 'b '(a b c d b e))
```

```{r include=FALSE}
knitscm::kill_scheme(handle)
```
