---
output:
  github_document:
    md_extensions: "+ignore_line_breaks"
---

# 4章 数遊び

```{r setup, include=FALSE}
library(knitscm)
gosh_path <- system2("which", "gosh", stdout = TRUE)
handle <- knitscm::run_scheme(gosh_path, "-i")
knitr::opts_chunk$set(handle = handle)
```

```{scm include=FALSE}
(load "./little.scm")
```

関数`o+`

```{scm}
(define o+
  (lambda (n m)
    (cond
     ((zero? m) n)
     (else (add1 (o+ n (sub1 m)))))))
```

```{scm}
(o+ 46 12)
```

関数`o-`

```{scm}
(define o-
  (lambda (n m)
    (cond
     ((zero? m) n)
     (else (sub1 (o- n (sub1 m)))))))
```

```{scm}
(o- 14 3)
```

関数`addtup`

```{scm}
(define addtup
  (lambda (tup)
    (cond
     ((null? tup) 0)
     (else (o+ (car tup) (addtup (cdr tup)))))))
```

```{scm}
(addtup '(3 5 2 8))
```

関数`×`

```{scm}
(define ×
  (lambda (n m)
    (cond
     ((zero? m) 0)
     (else (o+ n (× n (sub1 m)))))))
```

```{scm}
(× 12 3)
```

p.70の`tup+`

```{scm}
(define tup+
  (lambda (tup1 tup2)
    (cond
     ((and (null? tup1) (null? tup2))
      (quote ()))
     (else
      (cons (o+ (car tup1) (car tup2))
            (tup+ (cdr tup1) (cdr tup2)))))))
```

```{scm}
(tup+ '(3 7) '(4 6))
```

p.73のどんな2つのタップでも動く`tup+`

```{scm}
(define tup+
  (lambda (tup1 tup2)
    (cond
     ((null? tup1) tup2)
     ((null? tup2) tup1)
     (else
      (cons (o+ (car tup1) (car tup2))
            (tup+ (cdr tup1) (cdr tup2)))))))
```

```{scm}
(tup+ '(3 7) '(4 6 8 1))
```

関数`＞`

```{scm}
(define ＞
  (lambda (n m)
    (cond
     ((zero? n) #f)
     ((zero? m) #t)
     (else (＞ (sub1 n) (sub1 m))))))
```

```{scm}
(＞ 12 133)
```

```{scm}
(＞ 120 11)
```

```{scm}
(＞ 3 3)
```

関数`＜`

```{scm}
(define ＜
  (lambda (n m)
    (cond
     ((zero? m) #f)
     ((zero? n) #t)
     (else (＜ (sub1 n) (sub1 m))))))
```

```{scm}
(＜ 4 6)
```

```{scm}
(＜ 8 3)
```

```{scm}
(＜ 6 6)
```

関数`＝`

```{scm}
(define ＝
  (lambda (n m)
    (cond
     ((＞ n m) #f)
     ((＜ n m) #f)
     (else #t))))
```

```{scm}
(＝ 2 3)
```

```{scm}
(＝ 3 3)
```

関数`↑`

```{scm}
(define ↑
  (lambda (n m)
    (cond
     ((zero? m) 1)
     (else (× n (↑ n (sub1 m)))))))
```

```{scm}
(↑ 2 3)
```

関数`÷`

```{scm}
(define ÷
  (lambda (n m)
    (cond
     ((＜ n m) 0)
     (else (add1 (÷ (o- n m) m))))))
```

```{scm}
(÷ 15 4)
```

関数`length`

```{scm}
(define length
  (lambda (lat)
    (cond
     ((null? lat) 0)
     (else (add1 (length (cdr lat)))))))
```

```{scm}
(length '(ham and cheese on rye))
```

関数`pick`

```{scm}
(define pick
  (lambda (n lat)
    (cond
     ((zero? (sub1 n)) (car lat))
     (else (pick (sub1 n) (cdr lat))))))
```

```{scm}
(pick 4 '(lasagna spaghetti ravioli macaroni meatball))
```

関数`rempick`

```{scm}
(define rempick
  (lambda (n lat)
    (cond
     ((zero? (sub1 n)) (cdr lat))
     (else (cons (car lat)
                 (rempick (sub1 n)
                          (cdr lat)))))))
```

```{scm}
(rempick 3 '(hotdogs with hot mustard))
```

関数`no-nums`

```{scm}
(define no-nums
  (lambda (lat)
    (cond
     ((null? lat) (quote ()))
     ((number? (car lat)) (no-nums (cdr lat)))
     (else
      (cons (car lat)
            (no-nums (cdr lat)))))))
```

```{scm}
(no-nums '(5 pears 6 prunes 9 dates))
```

関数`all-nums`

```{scm}
(define all-nums
  (lambda (lat)
    (cond
     ((null? lat) (quote ()))
     ((number? (car lat))
      (cons (car lat)
            (all-nums (cdr lat))))
     (else
      (all-nums (cdr lat))))))
```

```{scm}
(all-nums '(5 pears 6 prunes 9 dates))
```

関数`eqan?`

```{scm}
(define eqan?
  (lambda (a1 a2)
    (cond
     ((and (number? a1) (number? a2))
      (= a1 a2))
     ((or (number? a1) (number? a2))
      #f)
     (else (eq? a1 a2)))))
```

```{scm}
(eqan? 2 2)
```

```{scm}
(eqan? 2 'a)
```

```{scm}
(eqan? 'a 'a)
```

関数`occur`

```{scm}
(define occur
  (lambda (a lat)
    (cond
     ((null? lat) 0)
     ((eq? (car lat) a)
      (add1 (occur a (cdr lat))))
     (else
      (occur a (cdr lat))))))
```

```{scm}
(occur 'a '(a b a c a e))
```

関数`one?`

```{scm}
(define one?
  (lambda (n) (= n 1)))
```

書き直した`rempick`

```{scm}
(define rempick
  (lambda (n lat)
    (cond
     ((one? n) (cdr lat))
     (else (cons (car lat)
                 (rempick (sub1 n)
                          (cdr lat)))))))
```

```{scm}
(rempick 3 '(lemon meringue salty pie))
```
