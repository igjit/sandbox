---
output:
  github_document:
    md_extensions: "+ignore_line_breaks"
---

# 8章 究極のlambda

```{r setup, include=FALSE}
library(knitscm)
gosh_path <- system2("which", "gosh", stdout = TRUE)
handle <- knitscm::run_scheme(gosh_path, "-i")
knitr::opts_chunk$set(handle = handle)
```

```{scm include=FALSE}
(load "./little.scm")
```

関数`rember-f`

```{scm results=FALSE}
(define rember-f
  (lambda (test? a l)
    (cond
     ((null? l) (quote ()))
     ((test? (car l) a) (cdr l))
     (else (cons (car l)
                 (rember-f test? a (cdr l)))))))
```

```{scm}
(rember-f = 5 '(6 2 5 3))
```

```{scm}
(rember-f eq? 'jelly '(jelly beans are good))
```

関数`eq?-c`

```{scm results=FALSE}
(define eq?-c
  (lambda (a)
    (lambda (x)
      (eq? x a))))
```

`eq?-c`を使った関数`eq?-salad`

```{scm results=FALSE}
(define eq?-salad
  (eq?-c 'salad))
```

```{scm}
(eq?-salad 'salad)
```

```{scm}
(eq?-salad 'tuna)
```

名前を付ける必要はない

```{scm}
((eq?-c 'salad) 'tuna)
```

`test?`を受け取って関数を返すように書き換えた`rember-f`

```{scm results=FALSE}
(define rember-f
  (lambda (test?)
    (lambda (a l)
      (cond
       ((null? l) (quote ()))
       ((test? (car l) a) (cdr l))
       (else (cons (car l)
                   ((rember-f test?) a (cdr l))))))))
```

```{scm}
((rember-f eq?) 'tuna '(tuna salad is good))
```

関数`insertL-f`

```{scm results=FALSE}
(define insertL-f
  (lambda (test?)
    (lambda (new old l)
      (cond
       ((null? l) (quote ()))
       ((test? (car l) old)
        (cons new (cons old (cdr l))))
       (else
        (cons (car l)
              ((insertL-f test?) new old (cdr l))))))))
```

```{scm}
((insertL-f eq?) 'x 'b '(a b c d))
```

関数`insertR-f`

```{scm results=FALSE}
(define insertR-f
  (lambda (test?)
    (lambda (new old l)
      (cond
       ((null? l) (quote ()))
       ((test? (car l) old)
        (cons old (cons new (cdr l))))
       (else
        (cons (car l)
              ((insertR-f test?) new old (cdr l))))))))
```

```{scm}
((insertR-f eq?) 'x 'b '(a b c d))
```

関数`insert-g`

```{scm results=FALSE}
(define insert-g
  (lambda (seq)
    (lambda (new old l)
      (cond
       ((null? l) (quote ()))
       ((eq? (car l) old)
        (seq new old (cdr l)))
       (else
        (cons (car l)
              ((insert-g seq) new old (cdr l))))))))
```

`insert-g`を使って定義した`insertL`

```{scm results=FALSE}
(define insertL
  (insert-g
   (lambda (new old l)
     (cons new (cons old l)))))
```

```{scm}
(insertL 'x 'b '(a b c d))
```

補助関数`seqS`

```{scm results=FALSE}
(define seqS
  (lambda (new old l)
    (cons new l)))
```

`seqS`を使って定義した`subst`

```{scm results=FALSE}
(define subst (insert-g seqS))
```

```{scm}
(subst 'x 'b '(a b c d))
```

関数`yyy`

```{scm results=FALSE}
(define seqrem
  (lambda (new old l)
    l))

(define yyy
  (lambda (a l)
    ((insert-g seqrem) #f a l)))
```

```{scm}
(yyy 'sausage '(pizza with sausage and bacon))
```

関数`atom-to-function`

```{scm results=FALSE}
(define atom-to-function
  (lambda (x)
    (cond
     ((eq? x (quote +)) o+)
     ((eq? x (quote ×)) ×)
     (else ↑))))
```

`atom-to-function`を使った`value`

```{scm include=FALSE}
(define o+ +)
(define × *)
(define ↑ expt)
(define operator cadr)
(define 1st-sub-exp car)
(define 2nd-sub-exp caddr)
```

```{scm results=FALSE}
(define value
  (lambda (nexp)
    (cond
     ((atom? nexp) nexp)
     (else
      ((atom-to-function (operator nexp))
       (value (1st-sub-exp nexp))
       (value (2nd-sub-exp nexp)))))))
```

```{scm}
(value '(1 + (2 ↑ 3)))
```

関数`multirember-f`

```{scm results=FALSE}
(define multirember-f
  (lambda (test?)
    (lambda (a lat)
      (cond
       ((null? lat) (quote ()))
       ((test? (car lat) a)
        ((multirember-f test?) a (cdr lat)))
       (else (cons (car lat)
                   ((multirember-f test?) a (cdr lat))))))))
```

```{scm}
((multirember-f eq?) 'tuna '(shrimp salad tuna salad and tuna))
```

`multirember-f`を使った関数`multirember-eq?`

```{scm results=FALSE}
(define multirember-eq?
  (multirember-f eq?))
```

```{scm}
(multirember-eq? 'tuna '(shrimp salad tuna salad and tuna))
```

補助関数`eq?-tuna`

```{scm results=FALSE}
(define eq?-tuna
  (eq?-c 'tuna))
```

関数`multiremberT`

```{scm results=FALSE}
(define multiremberT
  (lambda (test? lat)
    (cond
     ((null? lat) (quote ()))
     ((test? (car lat))
      (multiremberT test? (cdr lat)))
     (else (cons (car lat)
                 (multiremberT test? (cdr lat)))))))
```

```{scm}
(multiremberT eq?-tuna '(shrimp salad tuna salad and tuna))
```

関数`multirember&co`

```{scm results=FALSE}
(define multirember&co
  (lambda (a lat col)
    (cond
     ((null? lat)
      (col (quote ()) (quote ())))
     ((eq? (car lat) a)
      (multirember&co
       a
       (cdr lat)
       (lambda (newlat seen)
         (col newlat
              (cons (car lat) seen)))))
     (else
      (multirember&co
       a
       (cdr lat)
       (lambda (newlat seen)
         (col (cons (car lat) newlat)
              seen)))))))
```

収集子`a-friend`

```{scm results=FALSE}
(define a-friend
  (lambda (x y)
    (null? y)))
```

```{scm}
(multirember&co 'tuna '() a-friend)
```

```{scm}
(multirember&co 'tuna '(tuna) a-friend)
```

```{scm}
(multirember&co 'tuna '(and tuna) a-friend)
```

収集子`last-friend`

```{scm results=FALSE}
(define last-friend
  (lambda (x y)
    (length x)))
```

```{scm}
(multirember&co 'tuna '(strawberries tuna and swordfish) last-friend)
```

関数`multiinsertLR&co`

```{scm results=FALSE}
(define multiinsertLR&co
  (lambda (new oldL oldR lat col)
    (cond
     ((null? lat)
      (col (quote ()) 0 0))
     ((eq? (car lat) oldL)
      (multiinsertLR&co
       new oldL oldR
       (cdr lat)
       (lambda (newlat L R)
         (col (cons new (cons oldL newlat))
              (add1 L) R))))
     ((eq? (car lat) oldR)
      (multiinsertLR&co
       new oldL oldR
       (cdr lat)
       (lambda (newlat L R)
         (col (cons oldR (cons new newlat))
              L (add1 R)))))
     (else
      (multiinsertLR&co
       new oldL oldR
       (cdr lat)
       (lambda (newlat L R)
         (col (cons (car lat) newlat)
              L R)))))))
```

```{scm}
(multiinsertLR&co 'salty 'fish 'chips '(chips and fish or fish and chips) list)
```
